; SpeechLib for Minimal 64x4
;
; To be used with the TMS5220C Speech Synthesizer expansion card.
;
#require "slu4-min64x4-asm >= 1.1.0"

SPEECH_COMMAND_REG     = $FEC0
SPEECH_STATUS_REG      = $FEC1
SPEECH_BOARD_STATE_REG = $FEC2
SPEECH_RESET_WS_REG    = $FEC3

SPEECH_CMD_SPEAK_EXTERNAL   = b01100000
SPEECH_CMD_RESET            = b11110000

BOARD_STATE_BUSY            = b00000001       ; bit 0
BOARD_STATE_INTERUPT        = b00000010       ; bit 1
BOARD_STATE_READY           = b00000100       ; bit 2

SPEECH_STATUS_TALK          = b10000000
SPEECH_STATUS_BUFFER_LOW    = b01000000
SPEECH_STATUS_BUFFER_EMPTY  = b00100000

; Zero Page Usage
; Most apps us forst few bytes of zero page directly. 
; We will offset a little to ensure no collisionms.
.org $40 "ZERO_PAGE_APPS"
#mute

_speech_buffer_cur_ptr: .2byte 0
_speech_buffer_end_ptr: .2byte 0


#emit
.memzone USER_APPS
.align
; speech_init
;
;   Prepares the TMS5220 for speech
;
;   Arguments
;       None
;
speech_init:
    ; initialize buffer pointers
    miv 0,_speech_buffer_cur_ptr
    miv 0,_speech_buffer_end_ptr
    ; execute the TMS5220 init sequence documented in
    ; P7 of the TMS5220 data sheet
    ;
    ; First, send 9 bytes of all ones
    mib 9,.counter
    phsi $ff
.loop:
    jps _speech_send_cmd_data
    jps _wait_on_speech_board_busy
    nop nop nop nop nop nop
    deb .counter fne .loop              ; decrement counter and loop if zero not set
    pls
    ; now send reset command
    phsi SPEECH_CMD_RESET
    jps _speech_send_cmd_data
    pls
    ; now wait 300 µs
    phsi $40 jps _delay pls
    rts
.counter:   .byte 0


.align
; speak_synchronous
;   Uses Speak External command to synchonously speak data in passed buffer.
;   Function returns when speaking is done.
;
;   Arguments
;       sp+3 : Buffer Start address (2 bytes)
;       sp+5 : Buffer End address (2 bytes)
;
;   Retruns:
;       None
;
speak_synchronous:
    ; start speech
    phs2s 5+0
    phs2s 3+2
    jps speech_start
    pls2
    pls2
.buffer_loop:
    ; send 8 bytes until buffer done
    ciz 0,_speech_buffer_cur_ptr+0 fne .update_buffer
    ciz 0,_speech_buffer_cur_ptr+1 fne .update_buffer
    fpa .done_talking
.update_buffer:
    jps speech_update_buffer
    fpa .buffer_loop
.done_talking:
    rts



; speech_start
;   Starts the asynchronous speaking process on the TMS5220 witht he passed buffer. Fills the TMS5220
;   FIFO buffer with 16 bytes, then returns. Caller should call speech_update_buffer
;   every 5-10 ms to check in on speech process and update FIFO buffer if needed.
;   Speech completion status can be checked with speech_is_talking.
;
;   If talking is in progress when this function is called, it will wait for talking to
;   complete.
;
;   Arguments]
;       sp+3 : Buffer Start address (2 bytes)
;       sp+5 : Buffer End address (2 bytes)
;
.align
speech_start:
    ; first check if there is already something talking
    ciz 0,_speech_buffer_cur_ptr+0 fne .wait_for_talking
    ciz 0,_speech_buffer_cur_ptr+1 fne .wait_for_talking
    fpa _setup_talking
.wait_for_talking:
    jps speech_get_status
    ani SPEECH_STATUS_TALK cpi 0 fne .wait_for_talking
_setup_talking:
    ; copy buffer pointers
    ms2v 3,_speech_buffer_cur_ptr
    ms2v 5,_speech_buffer_end_ptr
    ; send speak external command
    phsi SPEECH_CMD_SPEAK_EXTERNAL
    jps _speech_send_cmd_data
    pls
    ; send 16 bytes
    jps _send_8_bytes
    jps _send_8_bytes
    ; and we are done
    rts

.align
speech_start_if_not_talking:
    ; first check if there is already something talking
    ciz 0,_speech_buffer_cur_ptr+0 fne .is_talking
    ciz 0,_speech_buffer_cur_ptr+1 fne .is_talking
    jps speech_get_status
    ani SPEECH_STATUS_TALK cpi 0 fne .is_talking
.start_talking:
    jpa _setup_talking
.is_talking:
    rts

; speech_update_buffer
;   Checks to see if there is an ongoing speech operation and send the TMS5220 more data if
;   it is ready to receive it.
;
;   Arguments
;       None
;
.align
speech_update_buffer:
    ; first, check to see if we have a in-progress buffer, signified by a non-zero pointer
    ciz 0,_speech_buffer_cur_ptr+0 fne .check_interupt
    ciz 0,_speech_buffer_cur_ptr+1 fne .check_interupt
    fpa .done
.check_interupt:
    ; check to see if the interupt is high.
    ldb SPEECH_BOARD_STATE_REG
    ; if interupt is not set (active low), then there is nothing to do
    ani BOARD_STATE_INTERUPT cpi BOARD_STATE_INTERUPT feq .done
    ; now check if the TMS5220 FIFO buffer is low
    ; check to see if talking stopped but we still have buffer left
    jps speech_get_status phs
    ani SPEECH_STATUS_TALK cpi 0 feq .talking_stopped
    pls
    ani SPEECH_STATUS_BUFFER_LOW
    cpi SPEECH_STATUS_BUFFER_LOW fne .done
    ; OK, we need to send 8 more byte
    jps _send_8_bytes
    fpa .done
.talking_stopped:
    pls                     ; pull the talking status that was pushed on stack
    ; TODO handle restarting speech on remaining buffer
.buffer_done:
    ; buffer is done. set pointers to null
    miv 0,_speech_buffer_cur_ptr
    miv 0,_speech_buffer_end_ptr
.done:
    rts





.align
; _send_8_bytes
;   Sends the next 8 bytes in the speech buffer. If less than
;   8 bytes left, will send just that.
_send_8_bytes:
    ; need a small delay to ensure we aren't sending bytes too soon
    ; after a previous TMS5220 interaction
    phsi $10 jps _delay pls
    mib 8,.counter
.send_loop:
    ; ennsure buffer is not done
    czz _speech_buffer_cur_ptr+1,_speech_buffer_end_ptr+1
    fne .wait
    czz _speech_buffer_cur_ptr+0,_speech_buffer_end_ptr+0
    fne .wait
    fpa .done
    ; wait for board to not be busy, then wait 10 µs
.wait:
    jps _wait_on_speech_board_busy
.send_byte:
    ; send current byte
    ldt _speech_buffer_cur_ptr
    stb SPEECH_COMMAND_REG              ; send the byte
    ; wait for the TMS9918A to catch up = t_wait = 10us => 80 clock cycles
    phsi $04 jps _delay pls
    ; adjust current ptr and counter
    inv _speech_buffer_cur_ptr          ; increment buffer pointer
    deb .counter fne .send_loop
.done:
    ; delay some before the next interaction
    phsi $10 jps _delay pls
    rts
.counter:   .byte 0


; _delay
;       Implements a delay. Performs 5 NOPs for each count.
; 
;       Arguments
;       sp+3 : deplay count (1 byte)
; 
_delay:
    lds 3
.wait:
    nop nop nop nop nop
    dec bne .wait
    rts


; _wait_on_speech_board_busy
;       returns when the speech board is not busy
;
;       Arguments
;           None
;
_wait_on_speech_board_busy:
.wait:
    ldb SPEECH_BOARD_STATE_REG              ; get board state
    ani BOARD_STATE_BUSY                    ; get the busy bit
    cpi 0 fne .wait                         ; wait until busy bit is off 
    rts

; _speech_send_cmd_data
;   sends a single byte to the TMS5220 after waiting for it
;   to become not busy
;
;   Arguments
;       SP+3 - the byte to send to the TMS5220
;
;   Returns
;       none
_speech_send_cmd_data:
    jps _wait_on_speech_board_busy
    lds 3
    stb SPEECH_COMMAND_REG              ; send value to command register
    rts


; speech_get_status
;   fetches the inernal status of the TMS5220C
;
;   Arguments
;       None
;
;   Returns
;       Register A - The TMS5220C status byte
;
speech_get_status:
    ; wait until board is not busy
    jps _wait_on_speech_board_busy
    ; write a byte to the status register initiate a fetch
    mib 1,SPEECH_STATUS_REG
    ; now wait until TMS5220 is not busy
    jps _wait_on_speech_board_busy
    nop
    ; and fetch the status
    ldb SPEECH_STATUS_REG
    phs                 ; save status byte to stack
    ; need to wait at least 12us before doing anything else
    phsi $04 jps _delay pls
    ; and return
    pls                 ; get status byte back
    rts
